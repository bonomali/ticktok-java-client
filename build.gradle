buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'


sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'io.ticktok'
version = '0.1.0'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/rockem/maven" }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.4"

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.4'
    compile "com.google.code.gson:gson:2.7"

    testCompile 'org.rockem:blink-http:0.5'
    testCompile "junit:junit:4.12"
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "amaziagur"
            name "amazia gur"
            email "amaziagur@gmail.com"
        }
    }

    scm {
        url "https://github.com/ticktok-io/ticktok-java-client"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'io.ticktok'
            artifactId 'ticktok-java-client'
            version '1.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An AWESOME lib. Really!')
                root.appendNode('name', 'ticktok java client')
                root.appendNode('url', 'https://github.com/ticktok-io/ticktok-java-client')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = 'ticktok-java-client'
        userOrg = 'ticktok-io'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ticktok-io/ticktok-java-client.git'
        version {
            name = '1.0.0'
            desc = '1.0.0'
            released  = new Date()
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}



